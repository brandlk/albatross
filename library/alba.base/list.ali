{: Copyright (C) Helmut Brandl  <helmut dot brandl at gmx dot net>

   This file is distributed under the terms of the GNU General Public License
   version 2 (GPLv2) as published by the Free Software Foundation. :}

use
    predicate
end


G:ANY

case class
    LIST[G]
create
    nil
    (^) (head:G, tail:[G])  -- [G] is a shorthand for LIST[G]
end

(+) (a,b: [G]): [G]
        -- The concatenation of the lists 'a' and 'b'.
    -> inspect a
       case nil   then b
       case h ^ t then h ^ (t + b)
       end

(-) (a:[G]): [G]
        -- The reversed list 'a'.
    -> inspect a
       case nil   then nil
       case h ^ t then -t + [h]
       end

head (a:[G]): G
        -- The first element of the list 'a'.
    require
        a as x ^ t
    ensure
        Result = inspect a
                 case h ^ _ then h
                 end
    end

tail (a:[G]): [G]
        -- The list 'a' with the first element removed.
    require
        a as x ^ t
    ensure
        Result = inspect a
                 case _ ^ t then t
                 end
    end


all(a,b,c:[G], x:G)
    ensure
        -- concatenation of lists
        a   + nil =  a
        nil + b   =  b
        x^a + b   =  x^(a + b)

        a + b + c  =  a + (b + c)

        -- list reversal
        (-nil = nil)
        (-[x] = [x])        -- [x,y,...] is a shorthand for 'x^y^...^nil'
        (-x^a = -a + [x])

        (-(a + b)  =  -b + -a)
    end
